module Arkham.Treachery.Cards.DreamsOfRlyeh (
  DreamsOfRlyeh (..),
  dreamsOfRlyeh,
) where

import Arkham.Prelude

import Arkham.Ability
import Arkham.Classes
import Arkham.Message
import Arkham.Modifier
import Arkham.SkillType
import Arkham.Treachery.Cards qualified as Cards
import Arkham.Treachery.Helpers
import Arkham.Treachery.Runner

newtype DreamsOfRlyeh = DreamsOfRlyeh TreacheryAttrs
  deriving anyclass (IsTreachery)
  deriving newtype (Show, Eq, ToJSON, FromJSON, Entity)

dreamsOfRlyeh :: TreacheryCard DreamsOfRlyeh
dreamsOfRlyeh = treachery DreamsOfRlyeh Cards.dreamsOfRlyeh

instance HasModifiersFor DreamsOfRlyeh where
  getModifiersFor (InvestigatorTarget iid) (DreamsOfRlyeh attrs) =
    pure $ toModifiers attrs $ do
      guard $ treacheryOnInvestigator iid attrs
      [SkillModifier SkillWillpower (-1), SanityModifier (-1)]
  getModifiersFor _ _ = pure []

instance HasAbilities DreamsOfRlyeh where
  getAbilities (DreamsOfRlyeh a) =
    [ restrictedAbility a 1 OnSameLocation $
        ActionAbility Nothing $
          ActionCost
            1
    ]

instance RunMessage DreamsOfRlyeh where
  runMessage msg t@(DreamsOfRlyeh attrs@TreacheryAttrs {..}) = case msg of
    Revelation iid source
      | isSource attrs source ->
          t <$ push (AttachTreachery treacheryId (InvestigatorTarget iid))
    UseCardAbility iid source 1 _ _ | isSource attrs source -> do
      push $ beginSkillTest iid source (InvestigatorTarget iid) SkillWillpower 3
      pure t
    PassedSkillTest _ _ source SkillTestInitiatorTarget {} _ _
      | isSource attrs source ->
          t
            <$ push (Discard (toAbilitySource attrs 1) $ toTarget attrs)
    _ -> DreamsOfRlyeh <$> runMessage msg attrs
